apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false //混淆选项
            multiDexEnabled true
            debuggable false
            zipAlignEnabled true
            pseudoLocalesEnabled true
            shrinkResources false
            consumerProguardFiles 'proguard-library.txt'
        }

    }

    //TODO 1  conflict script
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }


    lintOptions {
        abortOnError false
    }

}
//使用自定义sdk,源码jar包等作为首要启动项,类似Eclipse UserLibrary
gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        def path = "sysLibs"
        def bootClasspath = "-Xbootclasspath/p:"
        fileTree(dir: path, include: ['*.jar']).visit { element ->
            bootClasspath = bootClasspath + ":library/${path}/" + element.relativePath + File.pathSeparator
        }
        println "bootClasspath=" + bootClasspath
        options.compilerArgs.add(bootClasspath)
    }

}
dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'sysLibs')//TODO 2  conflict jar
    compileOnly fileTree(include: [
            'zxing3.3-*.jar'
    ], dir: 'libs')//引用依赖但不编译依赖,只支持jar包
    api(fileTree(dir: 'libs', exclude: [
            'android-support*.jar',
            'gson*.jar',
            'tbs.jar',
            'zxing3.3-*.jar',
            'recyclerview*.jar',
            'appcompat*.jar',
            'gprinter*.jar',
            'rx*.jar'
    ]
            , include: ['*.jar']
    ))
    //noinspection GradleDependency,GradleDynamicVersion,GradleDynamicVersion
    api "com.android.support:appcompat-v7:26.0.2"
    //noinspection GradleDependency,GradleDynamicVersion,GradleDynamicVersion
    api "com.android.support:recyclerview-v7:26.0.2"
    //noinspection GradleDynamicVersion
    api "com.android.support:percent:26.0.2"
    //noinspection GradleDynamicVersion
    api "com.android.support:cardview-v7:26.0.2"
    //noinspection GradleDynamicVersion
    api "com.android.support:design:26.0.2"
    implementation 'com.android.support:multidex:1.+'

    implementation 'com.tencent.bugly:crashreport_upgrade:latest.release'
//其中latest.release指代最新版本号，也可以指定明确的版本号，例如1.2.0
    implementation 'com.tencent.bugly:nativecrashreport:latest.release'
}
